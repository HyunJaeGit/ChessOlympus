buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
    mavenLocal()
    google()
    maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
  }
}

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  // IntelliJ IDEA에서 빌드 및 실행 시 출력 디렉토리 설정
  idea {
    module {
      outputDir = file('build/classes/java/main')
      testOutputDir = file('build/classes/java/test')
    }
  }
}

configure(subprojects) {
  apply plugin: 'java-library'
  java.sourceCompatibility = 21

  // [인코딩 설정] 컴파일 및 실행 시 한글 깨짐 방지 (UTF-8 고정)
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  tasks.withType(JavaExec) {
    jvmArgs += "-Dfile.encoding=UTF-8"
  }

  // [자산 리스트 생성] assets 폴더 내 파일 목록을 assets.txt에 자동 기록
  tasks.register('generateAssetList') {
    inputs.dir("${project.rootDir}/assets/")
    File assetsFolder = new File("${project.rootDir}/assets/")
    File assetsFile = new File(assetsFolder, "assets.txt")

    doLast {
      assetsFile.delete()
      fileTree(assetsFolder).collect { assetsFolder.relativePath(it) }.sort().each {
        assetsFile.append(it + "\n")
      }
    }
  }
  processResources.dependsOn 'generateAssetList'

  compileJava {
    options.incremental = true
  }
}

subprojects {
  version = "$projectVersion"
  ext.appName = 'HadesVsZeus'

  repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
    maven { url = 'https://jitpack.io' }
  }
}

eclipse.project.name = 'HadesVsZeus' + '-parent'
